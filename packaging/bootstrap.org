#+title: pspmacs-bootstrap
#+PROPERTY: header-args :tangle bootstrap.el :mkdirp t :results no :eval never
#+auto_tangle: t

* Bootstrap package manager
- We shall use Emacs's built-in =package.el= by default.

* Check archive freshness
#+begin_src emacs-lisp
  ;;; bootstrap.el --- bootstrap package manager -*- lexical-binding: t; -*-

  ;; package configuration
  ;; Code:
  (require 'time-date)
  (require 'package)
  (defcustom pspmacs/archives-stale-days 1
    "Days after which archives become stale."
    :type 'integer)

  (defun pspmacs/archive-refreshed-recently-p (archive)
    "If package ARCHIVE was initialized today.

  t only if ARCHIVE's time-stamp within last `pspmacs/archives-stale-days'"
    (let* ((today (decode-time nil nil t))
           (archive-path (expand-file-name
                          (format "archives/%s/archive-contents" archive)
                          package-user-dir))
           (last-update-time (decode-time (file-attribute-modification-time
                                           (file-attributes archive-path))))
           (delta (make-decoded-time :day pspmacs/archives-stale-days)))
      (cond ((not (file-readable-p archive-path)) nil)
            ((time-less-p (encode-time
                           (decoded-time-add last-update-time delta))
                          (encode-time today))
             nil)
            (t t))))

  (defun pspmacs/archives-refreshed-recently-p ()
    "All archives have been refreshed today."
    (interactive)
    (cl-every #'pspmacs/archive-refreshed-recently-p
              (mapcar #'car package-archives)))
#+end_src

* Emacs-30
- [[https://github.com/slotThe/vc-use-package][vc-use-package]] will get included in GNU/Emacs soon.
#+begin_src emacs-lisp
  (defun pspmacs/init-use-package ()
    "Emacs v29 can install packages from version control.

  vc-use-package is included in Emacs 30"
    (eval-and-compile
      (customize-set-variable 'use-package-compute-statistics t)
      (customize-set-variable 'use-package-always-ensure t)
      (customize-set-variable 'use-package-always-defer t)
      (customize-set-variable 'use-package-expand-minimally t))

    ;; NEXT Drop once released with GNU/Emacs (29 or 30)
    (when (version< emacs-version "30")
      (unless (package-installed-p 'vc-use-package)
        (package-vc-install "https://github.com/slotThe/vc-use-package"))
      (require 'vc-use-package)))
#+end_src

* Call
#+begin_src emacs-lisp
  (defun pspmacs/init-package-manager ()
    "Initialize /=package.el/= as the package manager"
    ;; package should store data locally.
    (customize-set-variable 'package-user-dir
                            (expand-file-name "packages" local-emacs-dir))
    (unless (file-exists-p package-user-dir) (mkdir package-user-dir t))
    ;; Paranoia
    ;; (add-to-list 'package-archives
    ;;              '("stable" . "https://stable.melpa.org/packages/"))
    ;; ("stable" . 70)

    ;; Additional package archives
    (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/"))
    (customize-set-variable 'package-archive-priorities
                            '(("gnu"    . 99)
                              ("nongnu" . 80)
                              ("melpa"  . 0)))

    (package-initialize)
    (unless (pspmacs/archives-refreshed-recently-p)
      (message "Refreshing package archivesâ€¦")
      (package-refresh-contents))
    (pspmacs/init-use-package))
#+end_src

* Inherit
#+begin_src emacs-lisp
  (pspmacs/load-inherit)
  ;;; bootstrap.el ends here
#+end_src
